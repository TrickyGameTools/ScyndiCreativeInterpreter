// License Information:
// SCI_Exe/IntLibs/SCI/Audio.scyndi
// Scyndi - Audio Link Up
// version: 23.09.21
// Copyright (C) 2023 Jeroen P. Broks
// This software is provided 'as-is', without any express or implied
// warranty.  In no event will the authors be held liable for any damages
// arising from the use of this software.
// Permission is granted to anyone to use this software for any purpose,
// including commercial applications, and to alter it and redistribute it
// freely, subject to the following restrictions:
// 1. The origin of this software must not be misrepresented; you must not
// claim that you wrote the original software. If you use this software
// in a product, an acknowledgment in the product documentation would be
// appreciated but is not required.
// 2. Altered source versions must be plainly marked as such, and must not be
// misrepresented as being the original software.
// 3. This notice may not be removed or altered from any source distribution.
// End License Information

Module

// Just making sure this comes first. GewoonNiks will contain 'nil' as result, but that doesn't matter!
Var GewoonNiks = Lua.SCI_InitAPI(Lua.SCI_StateName,"Audio")



Class TAudio
	ReadOnly String Tag
	ReadOnly Bool AutoKill
	
	Constructor(String _T)
		Tag = _T
		AutoKill = Left(_T,3)=="***"
	End
	
	Int Play(int Channel=0)
		Return Lua.Audio.PlayAudio(Tag,Channel)
	End
	
	Int Loop(int Channel=0,int Loops=0)
		If Loops==0
			Loops=-1
		End
		Return Lua.Audio.PlayAudio(Tag,Channel,Loops)
	End
	
	Destructor
		if AutoKill
			Lua.Audio.KillAudio(TAG)
		end
	end
End

Var Obtain(string Tag)
	If !Lua.Audio.HasAudio(Tag)
		Lua.error(sprintf("No audio loaded with tag '%s'",Tag))
	end
	Return new TAudio(Tag)
End

Var Load(string file,string Tag="")
	String RTag 
	RTag = Lua.Audio.LoadAudio(File,Tag)
	return Obtain(RTag)
End

Var LoadNew(string file, string Tag)
	If Lua.Audio.HasAudio(Tag)
		Return Obtain(Tag)
	Else
		Return Load(File,Tag)
	End
End

Const Delegate StopChannel = Lua.Audio.StopChannel
Const Delegate HaltChannel = Lua.Audio.StopChannel
Const Delegate Kill = Lua.Audio.KillAudio
Const Delegate Has = Lua.Audio.Has